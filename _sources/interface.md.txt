# SMT Interface

## Metalevel Functions

MaudeSE provides three meta-level functions, defined in [smt.check](https://github.com/postechsv/maude-se/blob/main/src/smt-check.maude): 

* {ref}`metaSmtCheck`
* {ref}`metaSmtSearch`
* {ref}`metaSmtSearchPath`

(metaSmtCheck)=
### metaSmtCheck

This function checks the satisfiability of SMT queries, as similar to `metaCheck`, but it can handle various theories 
(with uninterpreted functions) and generate satisfying assignments.

The usage of `metaSmtCheck` is similar to `metaCheck` except that it takes 
an SMT logic and a boolean flag as its arguments, where the flag decides whether to generate a satisfying assignment for the satisfiable case.
```maude
op metaSmtCheck : Module Term Logic -> SmtCheckResult .
eq metaSmtCheck(M:Module, TERM:Term, LOGIC:Logic) 
 = metaSmtCheck(M:Module, TERM:Term, LOGIC:Logic, false) .
  
op metaSmtCheck : Module Term Bool -> SmtCheckResult .
eq metaSmtCheck(M:Module, TERM:Term, FLAG:Bool) 
 = metaSmtCheck(M:Module, TERM:Term, auto, FLAG:Bool) .

op metaSmtCheck : Module Term Logic Bool -> SmtCheckResult
  [special (id-hook MetaLevelSmtOpSymbol    (metaSmtCheck)
            ...)] .
```

The check result is given as a term of sort `SmtCheckResult`, which can be one of `unknown`, `true`, `false`, or `{_}`.
The symbol `{_}` consists of a set of assignments, where each assignment is denoted as a term `_|->_`.

```maude
sort SmtCheckResult .
subsort Bool < SmtCheckResult .

op unknown : -> SmtCheckResult [ctor] .
op {_} : SatAssignmentSet -> SmtCheckResult [ctor] .

sort SatAssignment .
op _|->_ : Term Term -> SatAssignment [ctor] .

sort SatAssignmentSet .
subsort SatAssignment < SatAssignmentSet .
op empty : -> SatAssignmentSet [ctor] .
op _,_ : SatAssignmentSet SatAssignmentSet -> SatAssignmentSet [ctor comm assoc id: empty] .
```

(metaSmtSearch)=
### metaSmtSearch

This function performs symbolic reachability analysis using rewriting modulo SMT, similar to `metaSmtSearch`, but also supports folding.

The usage of `metaSmtSearch` is similar to `metaSmtSearch` except that it takes a logic, a folding flag, and a merging flag as its arguments.

```maude
op  metaSmtSearch : Module Term Term Condition Qid Bound Nat Qid Bool Bool ~> SmtResult2? .
ceq metaSmtSearch(M, T, T', COND, STEP, B, N, LOGIC, FOLD, MERGE)
  = metaSmtSearch(transform(M, VN + N'), ...)
if {AT, CD, VN} := abst(M, T, 0) /\ {AT', CD', N'} := abst(M, T', VN)
/\ {CND, CND'} := sep(COND) /\ SMT := $getSmt(CND') .

op metaSmtSearch : Module Term Term Condition Qid Bound Nat Qid ~> SmtResult2? .
eq metaSmtSearch(M, T, T', COND, STEP, B, N, LOGIC)
 = metaSmtSearch(M, T, T', COND, STEP, B, N, LOGIC, false, false) .

op metaSmtSearch : Module Term Term Condition Qid Bound Nat Qid Bool ~> SmtResult2? .
eq metaSmtSearch(M, T, T', COND, STEP, B, N, LOGIC, FOLD)
 = metaSmtSearch(M, T, T', COND, STEP, B, N, LOGIC, FOLD, false) .

op metaSmtSearch : Module Term Term Condition Term Qid Nat Bound Nat Qid Bool Bool ~> SmtResult2?
  [special (id-hook MetaLevelSmtOpSymbol     (metaSmtSearch)
            ...)] .
```
 
(metaSmtSearchPath)=
### metaSmtSearchPath

This function takes the same argument as `metaSmtSearch` and performs the same search, but returns a path to the solution state.

---

## Core Maude Functions

MaudeSE provides two core Maude-level functions, which are also defined in [smt.check](https://github.com/postechsv/maude-se/blob/main/src/smt-check.maude):

* {ref}`smtCheck`
* {ref}`simplifyFormula`

(smtCheck)=
### smtCheck

This function checks the satisfiability of an SMT formula. The usage is similar to `metaSmtCheck`. 


```maude
op smtCheck : BooleanExpr Logic -> SmtCheckResult .
eq smtCheck(B:BooleanExpr, LO:Logic) 
 = smtCheck(B:BooleanExpr, LO:Logic, false) .

op smtCheck : BooleanExpr Bool -> SmtCheckResult .
eq smtCheck(B:BooleanExpr, FLAG:Bool) 
 = smtCheck(B:BooleanExpr, auto, FLAG:Bool) .

op smtCheck : BooleanExpr -> SmtCheckResult .
eq smtCheck(B:BooleanExpr) 
 = smtCheck(B:BooleanExpr, false) .

op smtCheck : BooleanExpr Logic Bool -> SmtCheckResult 
    [special (id-hook SmtOpSymbol (smtCheck))] .
```

(simplifyFormula)=
### simplifyFormula

This function obtains a simplified but equivalent SMT formula.

```maude
op simplifyFormula : BooleanExpr -> BooleanExpr [special (id-hook SmtOpSymbol (simplify))] .
op simplifyFormula : IntegerExpr -> IntegerExpr [special (id-hook SmtOpSymbol (simplify))] .
op simplifyFormula : RealExpr -> RealExpr [special (id-hook SmtOpSymbol (simplify))] .
```

---

(generic)=
## Generic Interface

```{important}
This will be updated in the near future.
```